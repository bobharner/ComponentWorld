description = "Tapestry5 Component Search HerokuApp"

apply plugin: "java"
apply plugin: "application"
apply plugin: "idea"

mainClassName = "com.howardlewiship.tapx.heroku.JettyMain"
applicationName = "app"

sourceCompatibility = "1.5"
targetCompatibility = "1.5"

tapestryVersion = "5.3"

group = "org.apache.tapestry"
version = "1.0"

repositories {
    mavenCentral()

    mavenLocal()

    // All things JBoss/Javassist/Hibernate
    mavenRepo url: "http://repository.jboss.org/nexus/content/groups/public/"

    // For stable versions of the tapx libraries
    mavenRepo url: "http://howardlewisship.com/repository/"

    // For non-stable versions of the tapx libraries
    mavenRepo url: "http://howardlewisship.com/snapshot-repository/"

    // For access to Apache Staging (Preview) packages
    mavenRepo url: "https://repository.apache.org/content/groups/staging"
}

// This simulates Maven's "provided" scope, until it is officially supported by Gradle
// See http://jira.codehaus.org/browse/GRADLE-784

configurations {
    provided
}

sourceSets {
    main {
        compileClasspath += configurations.provided
    }
    test {
        compileClasspath += configurations.provided
        runtimeClasspath += configurations.provided
    }
}

dependencies {

    compile "org.apache.tapestry:tapestry-core:$tapestryVersion"

    // This adds automatic compression of JavaScript and CSS in production mode:
    compile "org.apache.tapestry:tapestry-yuicompressor:$tapestryVersion"

    compile "org.apache.cayenne:cayenne-server:3.0.2"

    compile "org.apache.derby:derby:10.8.1.2"

    compile "com.howardlewisship:tapx-heroku:1.2-SNAPSHOT"

    compile "commons-lang:commons-lang:2.6"

    // Uncomment this to add support for file uploads:
    // compile "org.apache.tapestry:tapestry-upload:5.3"

    provided "javax.servlet:servlet-api:2.5"

    provided "org.apache.tapestry:tapestry-javadoc:$tapestryVersion"

    testCompile "org.apache.tapestry:tapestry-test:$tapestryVersion"

    testCompile "org.testng:testng:5.14.9"

    testCompile "org.easymock:easymock:3.0"
}

test {
    useTestNG()

    options.suites("src/test/conf/testng.xml")

    systemProperties["tapestry.service-reloading-enabled"] = "false"
    systemProperties["tapestry.execution-mode"] = "development"

    maxHeapSize = "600M"

    jvmArgs("-XX:MaxPermSize=256M")

    enableAssertions = true
}


idea.module {
    scopes.PROVIDED.plus += configurations.provided
    javaVersion = "inherited"
}

task stage(dependsOn: ['installApp'])

// task wrapper(type: Wrapper) {
// 	gradleVersion = '1.0-milestone-7'
// }
